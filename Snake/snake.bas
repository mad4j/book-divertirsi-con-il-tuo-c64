
100 REM SKAKE - 2019
110 REM DANIELE.OLMISANI@GMAIL.COM

200 DX=1 : DY=40 
210 SS=1024+DY : OF=1000-DY : ES=SS+OF
220 CM=55296+DY
 
300 POKE 53280,13 : POKE 53281,5

400 PRINT "{CLEAR}{REVERSE ON}{BLACK} 0                                      "
410 H=OF/2 : T=H : G=5 : D=0
420 SC=0 : DS = 0

430 POKE 198,0: WAIT 198,1: GET K$

450 GOTO 1005

500 IF DS=0 THEN 525
505 SC=SC+DS : DS=0
508 IF SC<=0 THEN SC=0
510 PRINT "{HOME}{REVERSE ON}{BLACK}"SC;

525 POKE SS+H,D : POKE CM+H,0
545 H=H+(D=0)+DY*(D=1)-(D=2)-DY*(D=3)
590 IF H<0 THEN H=H+OF
600 IF H>OF THEN H=H-OF

610 IF G>0 THEN G=G-1 : GOTO 700
630 X=PEEK(SS+T)
635 POKE SS+T,32
640 T =T+(X=0)+DY*(X=1)-(X=2)-DY*(X=3)
675 IF T<0 THEN T=T+OF
678 IF T>OF THEN T=T-OF

680 X=PEEK(SS+H)
690 IF X<4 THEN 2000 
695 IF X<>32 THEN 1000

700 GET K$
710 IF K$="Q" THEN D=1 : DS=-1
720 IF K$="A" THEN D=3 : DS=-1
730 IF K$="O" THEN D=0 : DS=-1
740 IF K$="P" THEN D=2 : DS=-1

900 GOTO 500

1000 DS=15 : G=G+INT(SC/50)+1
1005 X=INT(RND(1)*OF)
1008 IF PEEK(SS+X)<>32 THEN 1005
1010 POKE SS+X,81
1015 POKE CM+X,2
1020 GOTO 700

2000 POKE SS+H,42 : POKE CM+H,2
2010 POKE 198,0: WAIT 198,1: GET K$
2020 GOTO 400